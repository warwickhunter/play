apply plugin: 'kotlin-multiplatform'

ext.ktor_version = "1.2.0-rc"
ext.coroutines_version = "1.2.1"

kotlin {
    targets {
        final def iOSTarget = System.getenv('SDK_NAME')?.startsWith("iphoneos") \
                              ? presets.iosArm64 : presets.iosX64

        fromPreset(iOSTarget, 'ios') {
            binaries {
                framework('SharedCode')
            }
        }

        fromPreset(presets.jvm, 'android')
    }

    sourceSets {
        commonMain
        androidMain
        iosMain
    }
}

dependencies {
    commonMainApi 'org.jetbrains.kotlin:kotlin-stdlib-common'
    commonMainImplementation("io.ktor:ktor-client-core:$ktor_version")
    commonMainImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-core-common:$coroutines_version"

    androidMainApi 'org.jetbrains.kotlin:kotlin-stdlib'
    androidMainImplementation("io.ktor:ktor-client-android:$ktor_version")
    androidMainImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines_version"

    iosMainImplementation("io.ktor:ktor-client-ios:$ktor_version")
    iosMainImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines_version"
}


// workaround for https://youtrack.jetbrains.com/issue/KT-27170
configurations {
    compileClasspath
}

task packForXCode(type: Sync) {
    final File frameworkDir = new File(buildDir, "xcode-frameworks")
    final String mode = project.findProperty("XCODE_CONFIGURATION")?.toUpperCase() ?: 'DEBUG'
    final def framework = kotlin.targets.ios.binaries.getFramework("SharedCode", mode)

    inputs.property "mode", mode
    dependsOn framework.linkTask

    from { framework.outputFile.parentFile }
    into frameworkDir

    doLast {
        new File(frameworkDir, 'gradlew').with {
            text = "#!/bin/bash\nexport 'JAVA_HOME=${System.getProperty("java.home")}'\ncd '${rootProject.rootDir}'\n./gradlew \$@\n"
            setExecutable(true)
        }
    }
}
tasks.build.dependsOn packForXCode
